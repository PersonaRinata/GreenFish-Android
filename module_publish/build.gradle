plugins {
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'//kapt插件，Arouter注解处理器需要
}
if (!isDebug){
    apply plugin:'com.android.library'
}else{
    apply plugin:'com.android.application'
}
android {
    namespace 'com.handsome.module.publish'
    compileSdk app_sdk.compileSdk

    defaultConfig {
        if (isDebug){
            applicationId "com.handsome.module.publish"
        }
        minSdk app_sdk.minSdk
        targetSdk app_sdk.targetSdk
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        //AGP8.0以下不需要java17。1.8或者11即可
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        //AGP8.0以下不需要java17。1.8或者11即可
        jvmTarget = '17'
    }
    //下面这串代码用于单模块调试
    //源集 - 用来设定java目录或者资源目录
    sourceSets{
        main{
            if(isDebug){
                //组件化模式
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                java.srcDirs = ['src/main/debug','src/main/java']
            }else{
                //集成模式
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //排除掉debug包的信息
                java{
                    exclude 'src/main/debug'
                }
            }
        }
    }
    buildFeatures {
        viewBinding true
    }
}
//Arouter注解处理器选项
kapt{
    arguments{
        arg("AROUTER_MODULE_NAME",project.getName())
    }
}
dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    //依赖utlis工具模块
    implementation(project(":lib_util"))
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.6'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation("com.squareup.retrofit2:retrofit:2.6.1")

    //依赖api通信模块
    implementation(project(":lib_api"))
    //依赖Arouter注解处理器
    kapt "com.alibaba:arouter-compiler:$versions.arouter_version"
}